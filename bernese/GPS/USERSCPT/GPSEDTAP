package GPSEDTAP;
@ISA = ("RUNBPE");
# ============================================================================
#
# Name    :  GPSEDTAP
#
# Purpose :  Prepare parallel pre-processing: residual screening:
#            - Single-difference, phase measurements
#
# PARAMs  :  PARAM2 - output file identifier (default: V_C)
#            PARAM3 - empty: stop if no PZH files are found
#
# Author  :  M. Meindl
# Created :  20-Oct-2003
#
# Changes :  31-Jan-2005 MM: New initPar call (w/o tmpFil)
#            11-Aug-2011 RD: Updated for version 5.2
#
# ============================================================================
use strict;

use File::Basename;

use lib $ENV{BPE};
use bpe_util qw(initPar_Bl initPar_Cl prtErr);

sub run {
  my $bpe = shift;

# Get variables
# -------------
  my ($ssss,
      $clu, $clu2, $dirBpe, $c,
      $param2, $param3,
      $dirPsh,   $dirOut,   $dirRes,   $dirEdt,   $dirNeq,   $dirSum,
      $extPsh,   $extOut,   $extRes,   $extEdt,   $extNeq,   $extSum) =
  $bpe->getKeys
     ('$S+0',
      'V_CLU', 'V_CLUEDT', 'DIR_BPEPRT', 'V_C',
      'PARAM2', 'PARAM3',
      'DIR_PSH', 'DIR_OUT', 'DIR_RES', 'DIR_EDT', 'DIR_NEQ', 'DIR_SUM',
      'EXT_PSH', 'EXT_OUT', 'EXT_RES', 'EXT_EDT', 'EXT_NEQ', 'EXT_SUM');


# Delete old files (OUT, RES, NEQ, EDT)
# -------------------------------------
  $clu = $clu2 if $clu2 ne "";
  my $cluster = $clu ne "" ? "${ssss}???" : "${ssss}????";

  my $edt = "EDT";
  if ( $param2 ne "" ) {
    $edt = defined $$bpe{$param2} ? $$bpe{$param2} : $param2;
  }
  unlink glob("${dirSum}WRN${cluster}.${extSum}");
  unlink glob("${dirOut}ED[FL]${cluster}.${extOut}");
  unlink glob("${dirRes}ED[FL]${cluster}.${extRes}");
  unlink glob("${dirEdt}EDT${cluster}.${extEdt}");
  unlink glob("${dirNeq}${edt}${cluster}.${extNeq}");

# Initialize parallel run
# -----------------------
  my $bpeFil = "${dirBpe}EDT${ssss}";

  my @obsFil = glob("${dirPsh}????${ssss}.${extPsh}");

  if ($clu ne "") {
    initPar_Cl($bpe,$bpeFil,@obsFil,0,$clu,0);
  } else {
    @obsFil = map { substr(basename($_),0,4) } @obsFil;
    initPar_Bl($bpe,@obsFil);
  }

# Stop if no CZH files found
# --------------------------
  if (@obsFil == 0 && $param3 eq "") {
    prtErr($bpe,"No $extPsh files found") and
      die "No $extPsh files found for session ${ssss}";
  }

}
