package CODSPPAP;
@ISA = ("RUNBPE");
# ============================================================================
#
# Name    :  CODSPPAP
#
# Purpose :  Prepare parallel CODSPP run
#
# PARAMs  :  PARAM2 - output file identifier (default: SPP)
#            PARAM3 - empty: stop if no CZH files are found
#
# Author  :  M. Meindl
# Created :  30-Oct-2003
#
# Changes :  31-Jan-2005 MM: New initPar call (w/o tmpFil)
#            11-Aug-2011 RD: Updated for version 5.2
#
# ============================================================================
use strict;

use File::Basename;

use lib $ENV{BPE};
use bpe_util qw(prtErr initPar_Bl initPar_Cl);

sub run {
  my $bpe = shift;

# Get variables
# -------------
  my ($ssss    , $yyssss  ,
      $param2  , $param3  , $clu     , $dirBpe,
      $dirCzh  , $dirOut  , $dirSmc  ,
      $extCzh  , $extOut  , $extSmc  ) =
  $bpe->getKeys
     ('$S+0'   , '$YSS+0' ,
      'PARAM2' , 'PARAM3' , 'V_CLU'  , 'DIR_BPEPRT',
      'DIR_CZH', 'DIR_OUT', 'DIR_SMC',
      'EXT_CZH', 'EXT_OUT', 'EXT_SMC');

# Delete old files (OUT, SMC)
# ---------------------------
  my $spp = "SPP";
  if ( $param2 ne "" ) {
    $spp = defined $$bpe{$param2} ? $$bpe{$param2} : $param2;
  }
  unlink "${dirSmc}${spp}${yyssss}.${extSmc}";
  if ($clu ne "") {
    unlink glob("${dirOut}${spp}${ssss}???.${extOut}");
  } else {
    unlink glob("${dirOut}${spp}${ssss}????.${extOut}");
  }

# Initialize parallel run
# -----------------------
  my $bpeFil = "${dirBpe}SPP${ssss}";
  my @czhFil = glob("${dirCzh}????${ssss}.${extCzh}");

  if ($clu ne "") {
    initPar_Cl($bpe,$bpeFil,@czhFil,0,$clu,0);
  } else {
    @czhFil = map { substr(basename($_),0,4) } @czhFil;
    initPar_Bl($bpe,@czhFil);
  }

# Stop if no CZH files found
# --------------------------
  if (@czhFil == 0 && $param3 eq "") {
    prtErr($bpe,"No CZH files found") and
      die "No CZH files found for session ${ssss}";
  }

}
