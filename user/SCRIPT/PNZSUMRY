package PNZSUMRY;
@ISA = ("RUNBPE");
# ============================================================================
#
# Name    :  PNZSUMRY
#
# Purpose :  Summarize the information from a PositionzPP run
#
# PARAMs  :
#
# ============================================================================
use strict;

use lib $ENV{BPE};
use bpe_util qw(prtMess prtErr);
use Time::Local;
use LINZ::GNSS;
use LINZ::GNSS::Util;
use LINZ::GNSS::SinexFile;
use LINZ::GNSS::Time qw/seconds_decimal_year/;

use JSON;

sub run 
{
    my $bpe = shift;
    my ($ssss,$dirout,$dircrd,$dirsnx)=$bpe->getKeys('$S+0','DIR_OUT','DIR_CRD','DIR_SNX');
    my $outname=$dirout;
    prtMess($bpe,"PositionzPP summary script run");
    eval
    {
        CreateSummary($dirout,$dircrd,$dirsnx,$ssss);
    };
    if( $@ )
    {
        my $msg=$@;
        prtErr($bpe,$msg);
    }
}

sub CreateSummary
{
    my( $dirout, $dircrd, $dirsnx,$ssss ) = @_;
    
    my $summfile="$dirout/SUMMARY.JSON";
    my $summary=GetSummary($dirout,$dircrd,$dirsnx,$ssss);
    my $js=new JSON;
    if( open(my $sf,">$summfile"))
    {
        print $sf $js->pretty->canonical->encode($summary);
        print $sf "\n";
        close $sf;
    }
}

sub GetSummary
{
    my( $dirout, $dircrd, $dirsnx, $ssss ) = @_;
    my $summary={};
    GetOrbitType($summary,$dirout);
    GetRef($summary,$dirout,$dircrd,$ssss);
    GetSinexCoords($summary,$dirsnx,$ssss);
    GetFinalStats($summary,$dirout,$dircrd,$ssss);
    GetHelmert($summary,$dirout,$dircrd,$ssss);
    GetBaselineWkt($summary);
    return $summary;
}

sub TimeSeconds
{
    my ($time) = @_;
    return 0 if ! $time || $time !~ /^\s*(\d\d\d\d)\-(\d\d)\-(\d\d)\s+(\d\d)\:(\d\d)\:(\d\d)\s*$/;
    return timegm($6,$5,$4,$3,$2-1,$1-1900);
}

sub GetOrbitType
{
    my ($self,$dirout) = @_;
    my $orbfile="$dirout/GETORB.OUT";
    my $orbtype='UNKNOWN';
    if(open(my $of, $orbfile ))
    {
        while( my $line=<$of> )
        {
            if( $line=~/^\s*File\s+type\:\s+ORB\s+subtype\:\s+(\S+)\s*$/i )
            {
                $orbtype=$1;
                last;
            }
        }
        close($of);
    }
    substr($orbtype,1)=lc(substr($orbtype,1));
    $self->{orbit_type}=$orbtype;
}

sub GetRef
{
    my ($self,$dirout,$dircd,$ssss) = @_;
    my $rffile="$dirout/GETREF.OUT";
    my $refstns={};
    my $start;
    my $end;
    my $rfcodes='';
    my $minoffset='';
    my $maxoffset='';
    my $nrefstn=0;
    if( open( my $rf, $rffile ) )
    {
        my $stn;
        while( my $line=<$rf> )
        {
            $line=~ s/\s*$//;
            $start=$' if $line =~ /Session\s+start\s*\:\s+/;
            $end=$' if $line =~ /Session\s+end\s*\:\s+/;
            if( $line=~/Adding\s+reference\s+station\s+(.*?)\:/ )
            {
                my $name=$1;
                $stn={markname=>$name};
                $refstns->{$name}=$stn;
                $rfcodes .= ' '.$name;
                $nrefstn++;
                next;
            }
            # Note: this is VERY tightly linked to the text created by the GETDATA script.
            # Currently ITRF isn't handled correctly in either, or in the station coordinate
            # models they are built from.  This line tests ITRF\d{4} rather than as it was
            # ITRF2008 as GETDATA has been amended to 2014, even though what it actually
            # generates is the cordinate from the SCM, currently (2014-04-07) ITRF2008
            if( $stn && $line=~/Calculated\s+ITRF(?:\d{4})?\s+coordinate\s+as\s+\[(.*)\]/)
            {
                my @crds=split(' ',$1);
                $stn->{xyz}=[$crds[0]+0.0,$crds[1]+0.0,$crds[2]+0.0];
                next;
            }
            if( $stn && $line=~ /Offset\s+(\d+\.\d+)km/ )
            {
                my $offset=$1+0;
                $stn->{offset}=$offset;
                $minoffset=$offset if $minoffset eq '' || $offset < $minoffset;
                $maxoffset=$offset if $maxoffset eq '' || $offset > $maxoffset;
            }
       }
        my $starttime=<$rf>;
        my $endtime=<$rf>;
    }

    $self->{reference_stations}=$refstns;
    my @marknames=sort {$refstns->{$a}->{offset} <=> $refstns->{$b}->{offset}}
            keys %$refstns;
    $self->{refstn_summary}={
        count=>$nrefstn,
        marknames=>\@marknames,
        minoffset=>$minoffset,
        maxoffset=>$maxoffset
    };

    my $startepoch=TimeSeconds($start);
    my $endepoch=TimeSeconds($end);
    my $duration=$endepoch-$startepoch;
    my $year=seconds_decimal_year(($startepoch+$endepoch)/2.0);
    $year=sprintf("%.3lf",$year)+0.0;

    $self->{session}=
    { 
        start=>$start,
        end=>$end,
        duration=>$duration,
        year=>$year
    };
    return;
}

sub GetFinalStats
{
    my ($self,$dirout,$dircd,$ssss) = @_;
    my $fsfile="$dirout/AFS$ssss.OUT";
    my $final={};
    if( open( my $fsf, $fsfile ) )
    {
        while(my $line=<$fsf>)
        {
            $final->{dof}=$1+0 if $line=~/^\s*DEGREE\s+OF\s+FREEDOM\s+\(DOF\)\s*\:\s*(\d+)/;
            $final->{seuw}=$1+0 if $line=~/^\s*CHI\*\*2\/DOF\s*\:\s*(\d+\.\d+)/;
        }
        close($fsf);
    }
    $self->{final_adjustment_statistics}=$final;
}

sub GetHelmert
{
    my ($self,$dirout,$dircd,$ssss) = @_;
    my $hmfile="$dirout/HLM$ssss.OUT";
    my $helmert={};
    my $ncrd=0;
    my $sum=[0.0,0.0,0.0];
    my $refstns=$self->{reference_stations};
    my @crds=[];
    if( open( my $hmf, $hmfile ) )
    {
        while( my $line=<$hmf> )
        {
            last if $line=~/RESIDUALS\s+IN\s+LOCAL\s+SYSTEM\s+\(/;
        }
        while( my $line=<$hmf> )
        {
            next if $line !~ /^\s*\|\s*\d+\s*\|\s*([\w\s]+?)\s*\|\s*(\w)\s*\w\s*\|([\-\s\d\.]+)\|/;
            my ($name,$flag,$crds)=($1,$2,$3);
            next if ! exists $refstns->{$name};
            my $refstn=$refstns->{$name};
            my ($n,$e,$u)=split(' ',$crds);
            $ncrd++;
            $sum->[0] += $e;
            $sum->[1] += $n;
            $sum->[2] += $u;
            $refstn->{helmert_flag}=$flag;
            $refstn->{residual_enu_mm}=[$e+0.0,$n+0.0,$u+0.0];
            push(@crds,$refstn->{residual_enu_mm});
        }
        close($hmf);
    }
    if( $ncrd )
    { 
        $sum->[0]/=$ncrd;
        $sum->[1]/=$ncrd;
        $sum->[2]/=$ncrd;
        foreach my $crd (@crds)
        {
            $crd->[0] -= $sum->[0];
            $crd->[1] -= $sum->[1];
            $crd->[2] -= $sum->[2];
        }
    }
}

sub _wrapLon { return $_[0] < 0 ? $_[0]+360.0 : $_[0]; }

sub _mmError { return sprintf("%.1f",sqrt($_[0])*1000)+0.0; }

sub GetSinexCoords
{
    my ($self,$dirsnx,$ssss)=@_;
    my $snxfile="$dirsnx/FIN$ssss.SNX";
    my $sfdata={};
    eval
    {
        my $sf=new LINZ::GNSS::SinexFile($snxfile);
        my %codelist=();

        foreach my $stn ($sf->stations)
        {
            # Assume just one solution for code..
            my $code=$stn->{code};
            croak("SINEX file contains more than one solution for station $code\n")
                if exists $codelist{$code};
            $codelist{$code}=1;
               
            my $xyz=$stn->{xyz};
            my $covxyz=$stn->{covar};
            my ($llh,$covllh)=LINZ::GNSS::Util::ConvertXYZToLLH($xyz,$covxyz);
            my $errenu=[_mmError($covllh->[0]->[0]),_mmError($covllh->[1]->[1]),_mmError($covllh->[2]->[2])];
            $sfdata->{$code}={
                markname=>$code,
                xyz=>$xyz,
                covariance_xyz=>$covxyz,
                llh=>[_wrapLon($llh->lon),$llh->lat,$llh->hgt],
                covariance_enu=>$covllh,
                error_enu_mm=>$errenu,
            };
        }
    };
    if( $@ )
    {
        $sfdata->{calculation_error}=$@;
    }
    $self->{user_stations} = $sfdata;
}

sub GetBaselineWkt
{
    my ($self)=@_;
    $self->{baseline_wkt}='';
    eval
    {
        my $markname=(keys %{$self->{user_stations}})[0];
        my $llh=$self->{user_stations}->{$markname}->{llh};
        my @lines=();
        foreach my $rfs (values %{$self->{reference_stations}})
        {
            my ($rfllh)=LINZ::GNSS::Util::ConvertXYZToLLH($rfs->{xyz});
            push(@lines,sprintf("(%.8f %.8f, %.8f %.8f)",
                    $llh->[0],$llh->[1],_wrapLon($rfllh->lon),$rfllh->lat));
        }
        $self->{baseline_wkt}='MULTILINESTRING('.
           join(',',@lines).')';
    };
}

1;

